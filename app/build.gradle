plugins {
	id 'com.android.application'
	id 'kotlin-android'
	id 'kotlin-kapt' // annotations: hilt
	id 'dagger.hilt.android.plugin'
}

android {
	compileSdk targetSdkVer
	// if not specified, latest + whatever external libs use will be used = a mess
	buildToolsVersion buildToolsVer

	// these config apply to all build types
	defaultConfig {
		// if not specified, package name from manifest will be used, will change when that one changes
		applicationId "asceapps.weatheria"
		minSdk minSdkVer
		targetSdk targetSdkVer
		versionCode 13 // don't need to change for internal app sharing
		versionName "0.8.0"

		vectorDrawables.useSupportLibrary = true

		// removes unused l10n in libs like AppCompat/play services/maps?
		resConfigs "en", "ar"//, "fr", "es", "zh", "de", "ja", "ru", "ko", "pt", "in"
	}

	buildFeatures {
		viewBinding true
	}

	buildTypes {
		// anything related to applicationId is only specified in app module
		// minifyEnabled (and supposedly similar properties) are controlled only by app module
		// no need to copy them into other modules
		debug {
			applicationIdSuffix ".debug"
		}
		staging {
			debuggable true
			minifyEnabled true
			applicationIdSuffix ".staging"
			proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "release-rules.pro", "staging-rules.pro"
		}
		release {
			minifyEnabled true
			shrinkResources true // is different from code shrinking (aka minifyEnabled)
			proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "release-rules.pro"
		}
	}

	compileOptions {
		// to use java 8 libs in api < 26. needed even with kotlin for time apis
		coreLibraryDesugaringEnabled true

		sourceCompatibility javaVer
		targetCompatibility javaVer
	}

	kotlinOptions {
		jvmTarget = javaVer
		// IR compiler: default (since AS 4.2) kotlin compiler backend (supposedly faster, needed for Compose)
		freeCompilerArgs += [
			"-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi", // many flow operators
			"-Xopt-in=kotlinx.coroutines.FlowPreview", // for debounce
		]
	}

	dynamicFeatures = [':gmap']
}

dependencies {
	implementation project(":shared")
	coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$desugarVer"

	// - no longer need to add kotlin stdlib here, included by default w/ plugin

	// coroutines
	implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutineVer"
	implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutineVer"

	// general & ui
	api "androidx.core:core-ktx:$coreVer" // also used by gmap
	implementation "androidx.recyclerview:recyclerview:$recyclerViewVer"
	implementation "androidx.viewpager2:viewpager2:$viewPager2Ver"
	implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVer"
	implementation "androidx.coordinatorlayout:coordinatorlayout:$coordinatorLayoutVer"
	implementation "androidx.swiperefreshlayout:swiperefreshlayout:$swipeRefreshVer"
	implementation "com.google.android.material:material:$materialVer"

	// to use viewModels delegate, new result system, and lots more
	implementation "androidx.activity:activity-ktx:$activityVer"
	api "androidx.fragment:fragment-ktx:$fragmentVer" // also used by gmap

	// split install
	implementation "com.google.android.play:core:$playCoreVer"
	implementation "com.google.android.play:core-ktx:$playCoreKtxVer"

	// viewModel & liveData
	// runtime offers lifecycleOwner val, new flow consumers (e.g repeatOnLifecycle), etc.
	implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVer"
	implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVer"

	// nav
	// runtime offers findNavController() and navArgs property
	implementation "androidx.navigation:navigation-runtime-ktx:$navVer"
	implementation "androidx.navigation:navigation-fragment-ktx:$navVer"
	implementation "androidx.navigation:navigation-ui-ktx:$navVer"

	// anim vector drawable
	// core offers listeners, interpolators, etc
	implementation "androidx.core:core-animation:$coreAnimationVer"
	implementation "androidx.vectordrawable:vectordrawable-animated:$vectorDrawableAnimVer"

	// new preferences (fragment, etc.)
	implementation "androidx.preference:preference-ktx:$preferenceVer"// flp (+ activity recognition + geofencing)

	// for get my location using fused location provider
	implementation "com.google.android.gms:play-services-location:$gmsLocationVer"

	// work manager
	implementation "androidx.work:work-runtime-ktx:$workVer"

	// hilt
	kapt "com.google.dagger:hilt-android-compiler:$hiltVer"
	implementation "com.google.dagger:hilt-android:$hiltVer"

	// hilt with jetpack (viewModel, workManager)
	kapt "androidx.hilt:hilt-compiler:$hiltCompilerVer"
	implementation "androidx.hilt:hilt-work:$hiltCompilerVer"
	// this f***y artifact is always behind
	implementation "androidx.hilt:hilt-lifecycle-viewmodel:$hiltCompilerOldVer"

	// multi screen size support
	implementation "com.intuit.sdp:sdp-android:$intuitVer"
	implementation "com.intuit.ssp:ssp-android:$intuitVer"
}
